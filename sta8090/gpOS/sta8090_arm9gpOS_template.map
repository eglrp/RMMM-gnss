MEMORY
{
  code(rx)                        : org = LR_CODE_BASE, len = LR_CODE_SIZE

  itcm_area(rx)                   : org = 0x0, len = 0x40000 - DATA_TCM_SIZE

  dtcm_area(w)                    : org = DATA_TCM_START, len = DATA_TCM_SIZE

  #if( OS_HEAP_AREA_SIZE == 0)
  os_stack_area(w)                : org = DATA_TCM_START + DATA_TCM_SIZE, len = 0
  #else
  os_stack_area(w)                : org = OS_HEAP_AREA_START, len = OS_HEAP_AREA_SIZE
  #endif
}

__dtcm_area_limit__           = DATA_TCM_START + DATA_TCM_SIZE;

#if( OS_HEAP_AREA_SIZE != 0)
__os_heap_area_start__        = ORIGIN(os_stack_area);
#endif
__os_stack_area_limit__       = ORIGIN(os_stack_area) + LENGTH(os_stack_area);

_code_alignment_ = 16;

SECTIONS
{
  . = 0;
  .text = .;

  .text :
  {
    __code_area_start__ = .;

    KEEP( *(.reset_vector_area))
  } > code

  constructors :
  {
    PROVIDE(__init_array_start = 0);
    KEEP( *(SORT(.init_array.*)))
    KEEP( *(.init_array))
    PROVIDE(__init_array_end = 0);
  } > code

  destructors :
  {
    PROVIDE(__fini_array_start = 0);
    KEEP( *(SORT(.fini_array.*)))
    KEEP( *(.fini_array))
    PROVIDE(__fini_array_end = 0);
  } > code

  .ARM.extab :
  {
    *(.ARM.extab* .gnu_linkonce.armextab.*)
  } > code


  .text :
  {
    *( .text.startup.*)
    *(.glue_7t)
    *(.glue_7)
    *(.gcc*)
  } > code

  .eh_frame :
  {
    . = ALIGN(16);
    __code_load_reg_start__ = .;
  } > code

  .itcm_text :
  {
    __code_exec_reg_start__ = .;
    KEEP( *(.os20_vector_table))

    *.o                       ( OS20_ISR_REGION)
    *interrupti.o             ( .text* .rodata*)
    *kerneli.o                ( .text* .rodata*)
    *ostime.o                 ( .text* .rodata*)

    // Region for ISRs for generic, OS20 code and OS20 needed LLDs and services
    *.o                       ( GENERIC_CODE_ISR_REGION)
    *.o                       ( GENERIC_DATA_ISR_REGION)
    *.o                       ( OS_ISR_REGION)
    *.o                       ( MTU_ISR_REGION)
    *.o                       ( VIC_ISR_REGION)
    *.o                       ( DATA_ISR_REGION)

    // Region for ISRs for GNSS mandatory LLDs and related services
    *.o                       ( GNSSDSP_ISR_REGION)
    *.o                       ( SSP_ISR_REGION)

    // Region for ISRs for optional LLDs and related services
    *.o                       ( ADC_ISR_REGION)
    ;*.o                       ( CAN_ISR_REGION)
    ;*.o                       ( EFT_ISR_REGION)
    ;*.o                       ( GPIO_ISR_REGION)
    ;*.o                       ( I2C_ISR_REGION)
    ;*.o                       ( MSP_ISR_REGION)
    *.o                       ( UART_ISR_REGION)
    *.o                       ( USB_ISR_REGION)

    // GCC Lib code needed in ISRs
    *_udivsi3.o                ( .text* .rodata*)
    *_arm_muldivdf3.o          ( .text* .rodata*)

    // NVM handling related
    #ifdef NVM_RAM
    *gps_nvm_backup.o          ( .text* .rodata*)
    #endif

    #ifdef NVM_NOR
    // Driver
    *lld_fsmc*.o               ( .text* .rodata*)
    // service
    *svc_fsmc.o               ( FLASH_MODIFY_REGION)
    #endif

    #if defined( NVM_SQI )
    // Driver
    *lld_sqi*.o                ( .text* .rodata*)
    // service
    *svc_sqi.o                ( FLASH_MODIFY_REGION)
    #endif
  } > itcm_area AT > code

  #if ( GNSSLIB_FAST == 1 )
  .itcm_text :
  {
    *svc.o                     ( .text* .rodata*)
    *svci.o                    ( .text* .rodata*)
    *                          ( OS20_FAST_REGION)
    *                          ( GNSS_FAST_REGION)
  } > itcm_area AT > code
  #endif

  #if ( STAGPS_FAST == 1 )
  .itcm_text :
  {
    *stagps_derivative.o       ( .text* .rodata*)
    *stagps_gravity.o          ( .text* .rodata*)

    *fixpoint.o                ( STAGPS_FAST_REGION)
  } > itcm_area AT > code
  #endif

  #if ( GNSSLIB_FAST == 1 ) || ( STAGPS_FAST == 1 )
  .itcm_text :
  {
    // These libraries must be in ITCM to let ISR be executed during flash erases
    // NOTE: No C library call can be done in ISRs!!!
    //*libgcc.a:*.o             ( .text .rodata)

    *libgcc.a:*_thumb*_case_*.o         ( .text* .rodata*)
    *libgcc.a:*divsi3*.o                ( .text* .rodata*)
    *libgcc.a:*_dvmd_tls*.o             ( .text* .rodata*)
    *libgcc.a:*di3.o                    ( .text* .rodata*)
    *libgcc.a:*divmod*.o                ( .text* .rodata*)
    *libgcc.a:*bpabi.o                  ( .text* .rodata*)
    *libgcc.a:*_clzsi2.o                ( .text* .rodata*)

    *libc_s.a:*memcpy.o                 ( .text* .rodata*)
    *libgcc.a:*truncdfsf2.o             ( .text* .rodata*)
    *libgcc.a:*addsubdf3.o              ( .text* .rodata*)
    *libgcc.a:*muldivdf3.o              ( .text* .rodata*)
    *libgcc.a:*cmpdf2.o                 ( .text* .rodata*)
    *libgcc.a:*fixdfsi.o                ( .text* .rodata*)
    *libgcc.a:*fixunsdfsi.o             ( .text* .rodata*)
    *libgcc.a:*cmpsf2.o                 ( .text* .rodata*)
    *libgcc.a:*muldivsf3.o              ( .text* .rodata*)

    *libm.a:*s_cos.o                    ( .text* .rodata*)
    *libm.a:*s_sin.o                    ( .text* .rodata*)
    *libm.a:*w_sqrt.o                   ( .text* .rodata*)
    *libm.a:*s_scalbn.o                 ( .text* .rodata*)


  } > itcm_area AT > code

  .ARM.exidx :
  {
    PROVIDE( __exidx_start = .);
    *( .ARM.exidx* .gnu_linkonce.armexidx.*)
    PROVIDE( __exidx_end = .);
  } > itcm_area AT > code
  #endif

  .itcm_text :
  {
    *position.o                         ( .data .data.* .ramtext .bss .bss.*)
    *fix.o                              ( .data .data.* .ramtext .bss .bss.*)
    *waas_correction.o                  ( .data .data.* .ramtext .bss .bss.*)
    *waas_iono.o                        ( .data .data.* .ramtext .bss .bss.*)
  } > itcm_area AT > code

  . = ALIGN(16);
  __code_exec_reg_end__ = .;

  __data_load_reg_start__ = __code_load_reg_start__ + __code_exec_reg_end__;

  .data : ALIGN(16)
  {
    __data_exec_reg_start__ = .;
    *(.data)
    . = ALIGN(16);
    *(.data.*)
    . = ALIGN(16);
    *(.ramtext)
    . = ALIGN(16);
    __data_exec_reg_end__ = .;
  } > dtcm_area AT > code

  .bss : ALIGN(16)
  {
    __bss_start__ = .;
    *(.bss)
    . = ALIGN(16);
    *(.bss.*)
    . = ALIGN(16);
    *(COMMON)
    . = ALIGN(16);
    __bss_end__ = .;
  } > dtcm_area

  __dtcm_area_end__ = .;

  #if( OS_HEAP_AREA_SIZE == 0)
  __os_heap_area_start__      = .;
  #endif

  .rodata :
  {
    *(.rodata.*)
  } > code
}

PROVIDE(end = .);
_end =      .;
